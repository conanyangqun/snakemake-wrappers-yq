# mosdepth test case
# This is a mini-workflow, can be called from other working directory.
# Usage: 
# snakemake --cores -s /xxx/Snakefile --config bam=xxx,s_id=xxx,sample_info=xxx,out_dir=xxx
# --wrapper-prefix file:///xxx/snakemake-wrappers-yq/

import csv
import os

# Get parameters from config
bam = config.get('bam', None)
sample_id = config.get('s_id', "test")
sample_info = config.get('sample_info', None) # tsv file, with s_id, bam
out_dir = config.get('out_dir', os.getcwd())

# Samples dictionary
samples = {} # s_id: bam
if bam or sample_info:
    if bam:
        samples[sample_id] = bam
    elif sample_info:
        with open(sample_info, 'r') as f:
            reader = csv.DictReader(f, delimiter='\t')
            for row in reader:
                samples[row['s_id']] = row['bam']
    else:
        raise ValueError(f"bam or sample_info must be provided, but got {bam} and {sample_info}")
else:
    # Demo case
    samples['demo'] = os.path.join(out_dir, "demo.bam")

# Rules
rule all:
    input:
        summaries = [os.path.join(out_dir, s_id, f"{s_id}.mosdepth.summary.txt") for s_id in samples.keys()]

rule download_demo:
    output:
        demo_bam = os.path.join(out_dir, "demo.bam"),
        demo_bai = os.path.join(out_dir, "demo.bam.bai")
    shell:
        """
        BAM_URL='https://github.com/brentp/mosdepth/raw/refs/heads/master/tests/nanopore.bam'
        BAI_URL='https://github.com/brentp/mosdepth/raw/refs/heads/master/tests/nanopore.bam.bai'
        
        # Download BAM file
        if command -v wget >/dev/null 2>&1; then
            wget -O nanopore.bam ${{BAM_URL}}
        elif command -v curl >/dev/null 2>&1; then
            curl -L -o nanopore.bam ${{BAM_URL}}
        else
            echo "wget or curl is required to download demo data"
            exit 1
        fi
        
        # Download BAM index file
        if command -v wget >/dev/null 2>&1; then
            wget -O nanopore.bam.bai ${{BAI_URL}}
        elif command -v curl >/dev/null 2>&1; then
            curl -L -o nanopore.bam.bai ${{BAI_URL}}
        else
            echo "wget or curl is required to download demo data"
            exit 1
        fi
        
        mv nanopore.bam {output.demo_bam}
        mv nanopore.bam.bai {output.demo_bai}
        """

rule run_mosdepth:
    input:
        bam = lambda wc: samples[wc.s_id],
        bai = lambda wc: samples[wc.s_id] + ".bai"
    output:
        summary = os.path.join(out_dir, "{s_id}", "{s_id}.mosdepth.summary.txt")
    threads: 4
    params:
        extras = "",
    wrapper:
        "bio/mosdepth"
