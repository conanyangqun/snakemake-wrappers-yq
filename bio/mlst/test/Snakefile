# mlst test case
# This is a mini-workflow, can be called from other working directory.
# Usage: 
# snakemake --cores 1 -s /path/to/Snakefile --config contigs=xxx out_dir=xxx
# --wrapper-prefix file:///path/to/snakemake-wrappers-yq/

import csv
import os

# Get parameters from config
contigs = config.get('contigs', None)
sample_id = config.get('s_id', "test")
sample_info = config.get('sample_info', None) # tsv file, with s_id, contigs
samples = {} # s_id: contigs
out_dir = config.get('out_dir', os.getcwd())

# Samples dictionary
if contigs or sample_info:
    if contigs:
        samples[sample_id] = contigs
    elif sample_info:
        with open(sample_info, 'r') as f:
            reader = csv.DictReader(f, delimiter='\t')
            for row in reader:
                samples[row['s_id']] = row['contigs']
    else:
        raise ValueError(f"contigs or sample_info must be provided, but got {contigs} and {sample_info}")
else:
    # Demo case
    samples['demo'] = os.path.join(out_dir, "example.fna")

# Create logs directory
os.makedirs(os.path.join(out_dir, "logs"), exist_ok=True)

# Rules
rule all:
    input:
        tsv_reports = [os.path.join(out_dir, f"{s_id}", f"{s_id}.mlst.tsv") for s_id in samples.keys()],
        json_reports = [os.path.join(out_dir, f"{s_id}", f"{s_id}.mlst.json") for s_id in samples.keys()]

rule download_demo:
    output:
        demo_contigs = os.path.join(out_dir, "example.fna")
    log: os.path.join(out_dir, "logs", "download_demo.log")
    shell:
        """
        CONTIGS_URL='https://github.com/tseemann/mlst/raw/refs/heads/master/test/example.fna'
        
        # Download contigs file
        if command -v wget >/dev/null 2>&1; then
            wget -O {output} ${{CONTIGS_URL}}
        elif command -v curl >/dev/null 2>&1; then
            curl -L -o {output} ${{CONTIGS_URL}}
        else
            echo "wget or curl is required to download demo data"
            exit 1
        fi
        """

# Define run_mlst_json rule
rule run_mlst_json:
    input:
        contigs = lambda wc: samples[wc.s_id]
    output:
        report = os.path.join(out_dir, "{s_id}", "{s_id}.mlst.tsv"),
        json = os.path.join(out_dir, "{s_id}", "{s_id}.mlst.json")
    threads: 1
    benchmark: os.path.join(out_dir, "{s_id}", "{s_id}.mlst.benchmark.txt")
    params:
        mlst = "mlst",
        extras = ""
    log: os.path.join(out_dir, "logs", "{s_id}.run_mlst_json.log")
    wrapper:
        "bio/mlst"
